#@TYPE: Machine
#@NAME: axxiaarm64
#@DESCRIPTION: Machine configuration for Axxia AXM5616 SoC
DEFAULTTUNE = "cortexa57"
require conf/machine/include/tune-cortexa57.inc

MACHINE_FEATURES += "usbhost ext2 vfat pci"

KERNEL_IMAGETYPE = "uImage"
KERNEL_ALT_IMAGETYPE = "Image"
KERNEL_DEVICETREE = "intel/axm5616-sim.dtb \
                     intel/axm5616-victoria.dtb \
		     "

SERIAL_CONSOLES = "9600;ttyAMA0"
IMAGE_FSTYPES += "tar.bz2 wic wic.bmap"
WKS_FILE ?= "axxiaarm64-usbimage.wks"
do_image_wic[depends] += "mtools-native:do_populate_sysroot"
IMAGE_BOOT_FILES ?= "uImage axm5616-victoria.dtb"

# multilib support
MULTILIBS ?= "multilib:lib32"
DEFAULTTUNE_virtclass-multilib-lib32 ?= "armv7athf-neon"

# The loader is flashed into other device separately and not part of the SD image
OSTREE_BOOTLOADER_INCLUDE ?= ""
OSTREE_SD_UBOOT_WIC1 ?= ""
OSTREE_SD_UBOOT_WIC2 ?= ""
OSTREE_SD_UBOOT_WIC3 ?= ""
OSTREE_SD_UBOOT_WIC4 ?= ""

DEFAULT_DTB = "axm5616-victoria.dtb"

# Because of the size of boot loader is ~64M and it must be added
# to 64
OSTREE_WKS_EFI_SIZE ?= "--size=128M"
OSTREE_FDISK_FSZ ?= "128"

OSTREE_UBOOT_CMD ?= "bootm"
INITRAMFS_IMAGE ??= ""
IMAGE_BOOT_FILES_append = " \
    ${@bb.utils.contains('DISTRO_FEATURES', 'ostree', 'boot.scr', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'ostree', '${INITRAMFS_IMAGE}-${MACHINE}.${INITRAMFS_FSTYPES};initramfs', '', d)} \
"
OSTREE_KERNEL ?= "uImage"
OSTREE_COPY_IMAGE_BOOT_FILES = "1"
OSTREE_BOOTSCR_PRECMD ?= "\
setenv loadaddr 0x10000000; \
setenv initrd_addr 0x14800000; \
setenv fdt_addr 0x2000000; \
setenv no_menu yes; \
setenv console ttyAMA0; \
setenv devtype usb; \
"

# For ustart.img
OSTREE_NETINST_DEV ?= "/dev/sda,/dev/sdb,/dev/mmcblk0,/dev/vda"
OSTREE_NETINST_ARGS ?= " \
    ${OSTREE_CONSOLE} bl=ufsd rdinit=/install \
    instdev=${OSTREE_NETINST_DEV} instname=wrlinux \
    instbr=\$BRANCH insturl=\$URL instab=${OSTREE_USE_AB} \
    instsf=1 instdate=BUILD_DATE \
"
